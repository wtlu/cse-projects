10 REM THIS IS A VARIANT OF THE CLASSIC BAGELS GAME WRITTEN BY
20 REM RESEK AND ROWE (THIS VERSION BY STUART REGES, 11/18/07)
30 LET Y = 0
40 PRINT "WELCOME TO BAGELS!"
50 PRINT
60 PRINT "WOULD YOU LIKE THE RULES (1 FOR YES, 2 FOR NO)?",
70 INPUT R
80 IF R=2 THEN 140
90 PRINT "I AM THINKING OF A THREE-DIGIT NUMBER.  TRY TO GUESS"
100 PRINT "MY NUMBER AND I WILL GIVE YOU CLUES AS FOLLOWS:"
110 PRINT "    PICA   - ONE DIGIT CORRECT BUT IN THE WRONG POSITION"
120 PRINT "    FERMI  - ONE DIGIT CORRECT AND IN THE RIGHT POSITION"
130 PRINT "    BAGELS - NO DIGITS CORRECT"
140 LET A1 = 1 + INT(9 * RND(0))
150 LET A2 = 1 + INT(9 * RND(0))
160 LET A3 = 1 + INT(9 * RND(0))
170 LET A = A1 * 100 + A2 * 10 + A3
180 PRINT "O.K.  I HAVE A NUMBER IN MIND."
190 REM THEY HAD A FOR LOOP TO ALLOW 20 GUESSES, BUT IT DEPENDED ON
200 REM USING A GOTO OUT OF THE LOOP (BAD)...REPLACED WITH COUNTER G
210 LET G = 1
220 PRINT "GUESS #", G, "?",
230 INPUT N
240 IF N >= 100 THEN 270
250 PRINT "YOUR GUESS HAS TO BE AT LEAST 3 DIGITS"
260 GOTO 220
270 IF N <= 999 THEN 300
280 PRINT "YOUR GUESS CAN'T BE MORE THAN THREE DIGITS"
290 GOTO 220
300 LET N1 = INT(N/100)
310 LET N2 = INT((N - N1 * 100) / 10)
320 LET N3 = N - N1 * 100 - N2 * 10
330 LET T1 = A1
340 LET T2 = A2
350 LET T3 = A3
360 LET F = 0
370 IF T1 <> N1 THEN 410
380 LET F = F + 1
390 LET T1 = -2
400 LET N1 = -1
410 IF T2 <> N2 THEN 450
420 LET F = F + 1
430 LET T2 = -2
440 LET N2 = -1
450 IF T3 <> N3 THEN 500
460 LET F = F + 1
470 LET T3 = -2
480 LET N3 = -1
490 IF F = 3 THEN 910
500 LET P = 0
510 IF T2 <> N1 THEN 550
520 LET P = P + 1
530 LET T2 = -2
540 LET N1 = -1
550 IF T3 <> N1 THEN 590
560 LET P = P + 1
570 LET T3 = -2
580 LET N1 = -1
590 IF T1 <> N2 THEN 630
600 LET P = P + 1
610 LET T1 = -2
620 LET N2 = -1
630 IF T3 <> N2 THEN 670
640 LET P = P + 1
650 LET T3 = -1
660 LET N2 = -1
670 IF T1 <> N3 THEN 710
680 LET P = P + 1
690 LET T1 = -2
700 LET N3 = -1
710 IF T2 <> N3 THEN 750
720 LET P = P + 1
730 LET T2 = -2
740 LET N3 = -1
750 FOR I = 1 TO F
760 PRINT "FERMI",
770 NEXT I
780 FOR I = 1 TO P
790 PRINT "PICA",
800 NEXT I
810 IF F + P > 0 THEN 830
820 PRINT "BAGELS",
830 PRINT
840 PRINT
850 LET G = G + 1
860 IF G > 20 THEN 880
870 GOTO 220
880 PRINT "OH WELL"
890 PRINT "THAT'S TWENTY GUESSES, MY NUMBER WAS", A
900 GOTO 930
910 PRINT "YOU GOT IT!"
920 LET Y = Y + 1
930 PRINT "PLAY AGAIN (1 FOR YES, 2 FOR NO)?",
940 INPUT R
950 IF R = 1 THEN 140
960 IF Y = 0 THEN 980
970 PRINT "A", Y, "POINT BAGELS BUFF!!"
980 PRINT "HOPE YOU HAD FUN.  BYE."
990 END
